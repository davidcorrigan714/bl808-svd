PERIPHERAL_FILE = {SOI ~ NEWLINE* ~ (!NEWLINE ~ ANY)* ~ NEWLINE ~ "="* ~ NEWLINE*
~ TABLE_TWO_COLUMN ~ NEWLINE* ~ (REGISTER ~ NEWLINE*)* ~ NEWLINE* ~ EOI}

TABLE_TWO_COLUMN = _{ (ROW_DIVIDER_TWO_CELL ~ (ROW_TWO_CELL ~ ROW_SPACER_TWO_CELL?)*)+ }
ROW_TWO_CELL = _{ " "* ~ ("|" ~ CELL_DATA_){2} ~ "|" ~ NEWLINE }
ROW_DIVIDER_TWO_CELL =  _{ " "* ~ ("+" ~ ("-" | "=")*){2} ~ "+" ~ NEWLINE  }
ROW_SPACER_TWO_CELL =  _{ " "* ~ ("+" ~ " "*){2} ~ "+" ~ NEWLINE  }


REGISTER = { REGISTER_HEADER ~ NEWLINE* ~ REGISTER_ADDRESS ~ (!ROW_DIVIDER_FIVE_CELL ~ ANY)* ~ TABLE }
REGISTER_HEADER = _{REGISTER_TITLE ~ NEWLINE ~ REGISTER_HEADER_DIV }
REGISTER_TITLE = {(!NEWLINE ~ ANY)*}
REGISTER_HEADER_DIV = _{ "-"*}

REGISTER_ADDRESS = _{ ADDRESS_LABEL ~ "0x" ~ ADDRESS_VALUE ~ NEWLINE }
ADDRESS_LABEL = _{"**"~(!"*" ~ ANY)*~"**"}
ADDRESS_VALUE = { ASCII_HEX_DIGIT* }

TABLE = { ROW_DIVIDER_FIVE_CELL ~ ROW_FIVE_CELL ~ (ROW_DIVIDER_FIVE_CELL ~ FIRST_FIELD_ROW ~ ROW_SPACER_FIVE_CELL?
~ ( ROW_FIVE_CELL ~ ROW_SPACER_FIVE_CELL?)*)+ ~ ROW_DIVIDER_FIVE_CELL }

ROW_FIVE_CELL = { " "* ~ ("|" ~ CELL_DATA){5} ~ "|" ~ NEWLINE }
ROW_DIVIDER_FIVE_CELL =  _{ " "* ~ ("+" ~ ("-" | "=")*){5} ~ "+" ~ NEWLINE?  }
ROW_SPACER_FIVE_CELL =  _{ " "* ~ ("+" ~ " "*){5} ~ "+" ~ NEWLINE  }

FIRST_FIELD_ROW = { " "* ~ "|" ~ BIT ~ ("|" ~ CELL_DATA){4} ~ "|" ~ NEWLINE }
CELL_DATA = { (!"|" ~ ANY)* }
CELL_DATA_ = _{ (!"|" ~ ANY)* }
BIT = _{((BIT_START ~ ":" ~ BIT_END) | BIT_ONLY)}
BIT_ONLY = @{ ASCII_DIGIT+ }
BIT_START = @{ ASCII_DIGIT+ }
BIT_END = @{ ASCII_DIGIT+ }

WHITESPACE = _{ " "}
